/*
 * Copyright 2015-2020 Real Logic Limited, Adaptive Financial Consulting Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.gradle.plugins.ide.eclipse.model.Classpath

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
    id 'net.researchgate.release' version '2.8.1'
    id "com.github.ben-manes.versions" version "0.28.0"
}

def checkstyleVersion = '8.28'
def hamcrestVersion = '2.2'
def junitVersion = '4.13'
def mockitoVersion = '3.3.0'
def hdrHistogramVersion = '2.1.12'
def jmhVersion = '1.23'

def aeronVersion = '1.25.1'
def agronaVersion = '1.3.0'
def sbeVersion = '1.16.1'
def artioJavaVersion = JavaVersion.VERSION_1_8
def artioGroup = 'uk.co.real-logic'
def iLink3Enabled = false

defaultTasks 'clean', 'build', 'javadoc', 'install'

ext {
    group = artioGroup
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    iLink3Enabled = Boolean.getBoolean("fix.core.iLink3Enabled")

    if (!project.hasProperty('repoUsername')) {
        repoUsername = ''
    }

    if (!project.hasProperty('repoPassword')) {
        repoPassword = ''
    }

    if (!project.hasProperty('repoUrl')) {
        repoUrl = ''
    }

    if (!project.hasProperty('snapshotUrl')) {
        snapshotUrl = ''
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    // Make sure that we use the correct JDK in Eclipse
    // There needs to be a JDK8 JVM installed in Eclipse before
    // importing the generated projects.
    plugins.withType(JavaBasePlugin) {
        eclipse.classpath.file.whenMerged { Classpath path ->
            path.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {
                it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8"
            }
        }
    }

    group = artioGroup
    version = version

    tasks.withType(JavaCompile) {
        sourceCompatibility = artioJavaVersion
        targetCompatibility = artioJavaVersion
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    dependencies {
        testCompile "org.hamcrest:hamcrest:${hamcrestVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
        testCompile "junit:junit:${junitVersion}"
    }

    checkstyle.toolVersion = "${checkstyleVersion}"

    test {
        if (JavaVersion.current().isJava9Compatible()) {
            jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')
        }

        testLogging {
            events 'skipped', 'failed'
            showStandardStreams = true
            exceptionFormat = 'full'
            afterSuite { desc, result ->
                if (!desc.parent) {
                    println "Results: ${result.resultType} (${result.testCount} tests, " +
                        "${result.successfulTestCount} successes, " +
                        "${result.failedTestCount} failures, " +
                        "${result.skippedTestCount} skipped)"
                }
            }
        }

        systemProperty 'fix.core.close_checker', 'true'
        systemProperty "fix.core.debug", System.getProperty("fix.core.debug")

        beforeTest { desc ->
            if (System.properties["printTestNames"] != null) {
                print "Executing test ${desc.name} [${desc.className}]"
            }
        }

        afterTest { desc, result ->
            if (System.properties["printTestNames"] != null) {
                println " with result: ${result.resultType}"
            }
        }
    }

    javadoc {
        title = '<h1>Artio</h1>'
        options.bottom = '<i>Copyright &#169; 2015-2020 Real Logic Limited. All Rights Reserved.</i>'
        if (JavaVersion.current().isJava10Compatible()) {
            options.addBooleanOption 'html5', true
        }
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    jar {
        manifest.attributes(
            'Implementation-Title': 'Artio',
            'Implementation-Version': "${version}",
            'Implementation-Vendor': 'Real Logic Limited'
        )
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        archiveClassifier.set 'tests'
        archiveBaseName.set "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    task allDeps(type: DependencyReportTask) {}

    configurations {
        tests
    }

    artifacts {
        archives javadocJar, sourcesJar, testJar
        tests testJar
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}

def validationXsdPath = project(':artio-codecs').projectDir.toString() + '/src/main/resources/fpl/sbe.xsd'

project(':artio-codecs') {
    apply plugin: 'com.github.johnrengelman.shadow'

    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    dependencies {
        compile "org.agrona:agrona:${agronaVersion}"
        compile "uk.co.real-logic:sbe-tool:${sbeVersion}"
        testCompile sourceSets.generated.output
    }

    compileGeneratedJava {
        dependsOn 'generateMessages'
        classpath += sourceSets.main.runtimeClasspath
    }

    task generateMessages(type: JavaExec) {
        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = sourceSets.main.runtimeClasspath
        systemProperties('sbe.output.dir': generatedDir,
            'sbe.target.language': 'Java',
            'sbe.validation.stop.on.error': 'true',
            'sbe.validation.xsd': validationXsdPath)
        args = [
            'src/main/resources/uk/co/real_logic/artio/messages/message-schema.xml',
            'src/main/resources/uk/co/real_logic/artio/storage/storage-message-schema.xml'
        ]
    }

    jar {
        from(sourceSets.generated.output) {
            include '**/*.class'
        }
    }

    jar.dependsOn compileGeneratedJava

    checkstyleGenerated.enabled = false
}

project(':artio-ilink3-codecs') {
    apply plugin: 'com.github.johnrengelman.shadow'

    def outputClasses = 'out/generated/classes'
    def sbePath = 'src/main/resources/uk/co/real_logic/artio/ilink/ilinkbinary.xml'
    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    dependencies {
        compile "org.agrona:agrona:${agronaVersion}"
        compile "uk.co.real-logic:sbe-tool:${sbeVersion}"
        testCompile sourceSets.generated.output
    }

    if (iLink3Enabled) {
        compileGeneratedJava {
            dependsOn 'generateMessages', 'copySbeIr'
            sourceCompatibility = artioJavaVersion
            targetCompatibility = artioJavaVersion
            options.deprecation = true
            classpath += sourceSets.main.runtimeClasspath
        }

        task generateMessages(type: JavaExec) {
            main = 'uk.co.real_logic.sbe.SbeTool'
            classpath = sourceSets.main.runtimeClasspath
            systemProperties(
                'sbe.output.dir': generatedDir,
                'sbe.target.language': 'Java',
                'sbe.java.generate.interfaces': 'true',
                'sbe.generate.ir': 'true')
            args = [ sbePath ]
        }

        task copySbeIr (type: Copy) {
            from generatedDir.absolutePath
            include 'ilinkbinary.sbeir'
            into outputClasses + '/iLinkBinary'
        }
    }

    jar {
        from(sourceSets.generated.output) {
            include '**/*.class'
        }
        from(outputClasses) {
            include 'iLinkBinary/ilinkbinary.sbeir'
        }
    }

    jar.dependsOn compileGeneratedJava
    
    checkstyleGenerated.enabled = false
}


project(':artio-ilink3-impl') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compile project(':artio-ilink3-codecs')
        compile project(':artio-codecs')
        compile "io.aeron:aeron-client:${aeronVersion}"
    }

    idea {
        dependencies {
            compile project(':artio-ilink3-codecs').sourceSets.generated.output
            compile project(':artio-codecs').sourceSets.generated.output
        }
    }

    compileJava.enabled = iLink3Enabled
    compileTestJava.enabled = iLink3Enabled
    test.enabled = iLink3Enabled
    javadoc.enabled = iLink3Enabled
}

project(':artio-session-codecs') {
    dependencies {
        compile project(path: ':artio-codecs')
    }

    def generatedDir = file("$buildDir/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    compileGeneratedJava {
        dependsOn 'generateCodecs'
        classpath += sourceSets.main.runtimeClasspath
    }

    task generateCodecs(type: JavaExec) {
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.runtimeClasspath
        args = [generatedDir, 'src/main/resources/session_dictionary.xml']
        outputs.dir generatedDir
        systemProperty("fix.codecs.flyweight", "true")
    }

    jar {
        from(sourceSets.generated.output) {
            include '**/*.class'
        }
    }

    jar.dependsOn compileGeneratedJava
}

project(':artio-session-fixt-codecs') {
    dependencies {
        compile project(path: ':artio-codecs')
    }

    def generatedDir = file("$buildDir/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    compileGeneratedJava {
        dependsOn 'generateCodecs'
        classpath += sourceSets.main.runtimeClasspath
    }

    task generateCodecs(type: JavaExec) {
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.runtimeClasspath
        systemProperty("fix.codecs.parent_package", "uk.co.real_logic.artio.fixt")
        args = [generatedDir, 'src/main/resources/FIXT11.xml']
        outputs.dir generatedDir
    }

    jar {
        from(sourceSets.generated.output) {
            include '**/*.class'
        }
    }

    jar.dependsOn compileGeneratedJava
}

project(':artio-core') {
    dependencies {
        testCompile project(path: ':artio-codecs', configuration: 'tests')
        testCompile project(':artio-session-codecs')
        compile project(':artio-codecs')

        compile "io.aeron:aeron-client:${aeronVersion}"
        compile "io.aeron:aeron-driver:${aeronVersion}"
        compile "io.aeron:aeron-archive:${aeronVersion}"
        compile "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
    }

    test {
        systemProperties('java.net.preferIPv4Stack': true)
    }

    idea {
        dependencies {
            testCompile project(':artio-session-codecs').sourceSets.generated.output
            compile project(':artio-codecs').sourceSets.generated.output
        }
    }
}

project(':artio-system-tests') {
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceSets {
        perf
    }

    dependencies {
        testCompile project(path: ':artio-core', configuration: 'tests')
        testCompile project(path: ':artio-codecs', configuration: 'tests')

        testCompile project(path: ':artio-session-codecs', configuration: 'tests')
        testCompile project(path: ':artio-session-fixt-codecs', configuration: 'tests')

        compile project(':artio-session-codecs')
        compile project(':artio-session-fixt-codecs')

        compile project(path: ':artio-core')

        perfCompile project
        perfCompile "org.openjdk.jmh:jmh-core:${jmhVersion}"
        annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:${jmhVersion}"

        idea {
            dependencies {
                compile project(':artio-session-codecs').sourceSets.generated.output
                compile project(':artio-session-fixt-codecs').sourceSets.generated.output
            }
        }
    }

    idea.module {
        scopes.COMPILE.plus += [configurations.perfCompile]
    }

    test {
        systemProperties(
            'aeron.dir.warn.if.exists': 'false',
            'aeron.use.windows.high.res.timer': 'true',
            'aeron.term.buffer.sparse.file': 'true')
    }

    task benchmarks(dependsOn: perfClasses) {
        shadowJar {
            archiveClassifier.set "benchmarks"

            from sourceSets.main.output
            from sourceSets.perf.output
            from project.configurations.perfRuntime

            manifest.attributes('Main-Class': 'org.openjdk.jmh.Main')
        }

        doLast {
            shadowJar
        }
    }
}

project(':artio-ilink-system-tests') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        testCompile project(path: ':artio-core', configuration: 'tests')
        testCompile project(path: ':artio-codecs', configuration: 'tests')
        testCompile project(path: ':artio-system-tests', configuration: 'tests')

        compile project(':artio-session-codecs')
        compile project(path: ':artio-core')
        compile project(path: ':artio-ilink3-impl')

        idea {
            dependencies {
                compile project(':artio-ilink3-codecs').sourceSets.generated.output
            }
        }
    }

    compileJava.enabled = iLink3Enabled
    compileTestJava.enabled = iLink3Enabled
    javadoc.enabled = iLink3Enabled

    test {
        onlyIf { iLink3Enabled }
        systemProperties(
            'aeron.dir.warn.if.exists': 'false',
            'aeron.use.windows.high.res.timer': 'true',
            'aeron.term.buffer.sparse.file': 'true')
    }
}

project(':artio-samples') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compile project(':artio-core')
        compile project(':artio-session-codecs')
    }

    shadowJar {
        archiveFileName = 'samples.jar'
    }

    build.doLast {
        shadowJar
    }
}

subprojects {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: repoUrl) {
                    authentication(userName: repoUsername, password: repoPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: repoUsername, password: repoPassword)
                }

                pom.project {
                    name = 'artio'
                    packaging = 'jar'
                    description = 'High-Performance FIX Gateway'

                    url = 'https://github.com/real-logic/artio'

                    scm {
                        connection = 'scm:git:https://github.com/real-logic/artio.git'
                        developerConnection = 'scm:git:https://github.com/real-logic/artio.git'
                        url = 'https://github.com/real-logic/artio.git'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'RichardWarburton'
                            name = 'Richard Warburton'
                            email = 'richard.warburton@gmail.com'
                            url = 'https://github.com/RichardWarburton'
                        }
                        developer {
                            id = 'mjpt777'
                            name = 'Martin Thompson'
                            email = 'mjpt777@gmail.com'
                            url = 'https://github.com/mjpt777'
                        }
                    }
                }
            }
        }
    }
}

task uploadToMavenRepository {
    dependsOn 'artio-core:uploadArchives',
        'artio-samples:uploadArchives',
        'artio-system-tests:uploadArchives',
        'artio-codecs:uploadArchives',
        'artio-session-codecs:uploadArchives',
        'artio-session-fixt-codecs:uploadArchives'
}

wrapper {
    gradleVersion = '6.1.1'
    distributionType = 'ALL'
}
